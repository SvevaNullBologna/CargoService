System cargoservice_overview 
 
//MESSAGES AND EVENTS 
//Companysimulator -> CargoService
Request loadrequest : loadrequest(PID)
//CargoService -> ProductService 
Request getProduct : product(ID) 
//ProductService -> CompanySimulator 
Reply getProductAnswer : product(JSonString) for getProduct
//CargoService -> CargoService
Dispatch accepted : accepted(PID, Weight, Slot)
Dispatch refused : refused(PID, Weight)
//SonarService -> Whoever is listening
Event productDetected : productDetected(T)
Event anomalyDetected : anomalyDetected(T)
Event anomalyFixed : anomalyFixed(T)
//CargoService -> cargorobot
Dispatch command : command(X,Y,DIR)
//CargoRobot -> BasicRobot 
Request engage : engage(device, length)
Request moverobot : moverobot(x, y)
Dispatch setdirection : dir( D )  //up|down|left|right
Dispatch cmd : cmd(C)
//basicRobot -> CargoRobot
Reply engagedone : engagedone(E)
Reply engagerefused : engagerefused(E) 
Reply moverobotdone : moverobotdone(T)
Reply moverobotfailed : moverobotfailed(T)
//basicRobot -> whoever is listening 
Event alarm : alarm(reason)
//CargoRobot -> webgui
Dispatch update : update(U)

//CargoRobot -> Whoever is listening
Event finishedtransport : finishedtransport(T)


Context ctx_cargoservice 			ip [host="localhost" port=8111]
Context ctx_productservice 			ip [host="localhost" port=8001]
Context ctx_basicrobot 				ip [host= "localhost"port=8020]
Context ctx_companysim 				ip [host="localhost" port=8003]
Context ctx_sonarservice			ip [host="localhost" port=8004]

ExternalQActor basicrobot context ctx_basicrobot
ExternalQActor productservice context ctx_productservice 


QActor companysimulator context ctx_companysim{
 	State s0 initial {
 		println("$name STARTS") color cyan
  	}Goto sendRequest
  	
  	State sendRequest{
  		println("$name sending request ") color cyan
  		request cargoservice -m loadrequest : loadrequest(10)
  		delay 2000
  	}
}

QActor cargoservice context ctx_cargoservice{
	import "main.java.Slots"
	import "main.java.Slot"
	import "main.java.domain.Product"
	
	[#
		val MaxLoad = 1000
		lateinit var S: Slots
		var Cur_HoldWeight = 0

		var Cur_Slot_ID = -1
		var Cur_PID = -1
		var Cur_Weight = -1
		
	#]
	State s0 initial{
		println("cargoservice STARTS") color magenta
		[#
			S = Slots()
		#]
	}
	Goto waitrequest
	
	State waitrequest{
		println("WAITING FOR LOAD REQUEST...") color magenta
	} 
	Transition t0 whenRequest loadrequest -> getweight
	
	State getweight{
		printCurrentMessage
		onMsg(loadrequest: loadrequest(PID)){
			[# Cur_PID = payloadArg(0).toInt() #]
			println("received request with PID : $Cur_PID") color magenta
			println("sending request to productservice") color magenta
			request productservice -m getProduct : getProduct($Cur_PID)
		}
	}
	Transition t1 
	whenReply getProductAnswer -> checkProdAnswer
	
	State checkProdAnswer{
		printCurrentMessage
		onMsg(getProductAnswer : product(PJson)){
			[#
				val jsonStr = payloadArg(0)
				val p = Product(jsonStr)
				Cur_Weight = p.getWeight()
				Cur_PID = p.getProductId()
			#]
		}
	}
	Goto validateRequest if [# Cur_Weight > 0 #] else managerefusal
	
	State managerefusal{
		println("Request refused. Back to wait.") color magenta
	}Goto waitrequest
	
	State validateRequest{
		[# 
			Cur_Slot_ID = S.getAvaiableSlotID()
			val canLoad = Cur_Weight > 0 && (Cur_HoldWeight + Cur_Weight) <= MaxLoad && Cur_Slot_ID != -1 
		#]
			if[#canLoad#]{
				[#
					val T = "to load $Cur_Slot_ID"
				#]
				forward webguimock -m update : update($T)
				autodispatch accepted : accepted($Cur_PID, $Cur_Weight, $Cur_Slot_ID)
			}
			else{
				autodispatch refused : refused($Cur_PID, $Cur_Weight)
			}	 
	}
	Transition t2 
	whenMsg accepted -> waitForProduct
	whenMsg refused -> managerefusal
	
State waitForProduct{
		println("REQUEST ACCEPTED. Waiting for product on IOPort...") color magenta
	}
	Transition t3
	whenEvent productDetected -> serveloadrequest
	
	State serveloadrequest{
		println("Product detected. Moving robot...") color magenta
		[# 
			val DestinationX = S.getPickupPositionXById(Cur_Slot_ID)
			val DestinationY = S.getPickupPositionYById(Cur_Slot_ID)
			val Direction = S.getSlotPickupDirectionById(Cur_Slot_ID)
		#]
		forward cargorobot -m command : command($DestinationX, $DestinationY, $Direction)
	}Goto waitendofrequest
	
	State waitendofrequest{
		println("Waiting for robot to finish its task...") color magenta
		delay 300 	
	}
	Transition t4 
	whenEvent finishedtransport -> lastoperations 
	
	State lastoperations{
		printCurrentMessage
		onMsg(finishedtransport : finishedtransport(T)){
			[# val msg = payloadArg(0).toString()#]
			if[# msg=="failure" #]{
				println("There was a fatal error with the load. Load request rejected") color magenta
				
			}
			else{
				[#
					S.registerProductInSlot(Cur_Slot_ID)
				  	Cur_HoldWeight = Cur_HoldWeight + Cur_Weight 
				  	val T = "loaded to $Cur_Slot_ID"
				#]
				println("product loaded successfully...")
				forward webguimock -m update : update($T)
			}
			[#
				Cur_Slot_ID = -1
				Cur_PID = -1
				Cur_Weight = -1
			#]
		}
		
	}Goto waitrequest
	
	
}

QActor cargorobot context ctx_cargoservice {
	[#
		val StepTime = 350
		val HomeX = 0
		val HomeY = 0 
		val IoX = 5
		val IoY = 0

		var DestSlotX = 0
		var DestSlotY = 0
		var CurrentDestX = 0
		var CurrentDestY = 0
		
		var Direction = "up"
		

	#]

	State s0 initial {
		println("$name RUNNING") color magenta
	}Goto engageRobot

	State engageRobot {
		println("Engaging basic-robot...")
		request basicrobot -m engage : engage(cargorobot, $StepTime)
	}
	Transition t0
	whenReply engagedone -> waitForCommand
	whenReply engagerefused -> retryEngage

	State retryEngage {
		println("Engage refused, retrying...")
		delay 500
	}Goto engageRobot

	State waitForCommand {
		println("Waiting for command")
	}
	Transition t1 whenMsg command -> prepareDelivery

	State prepareDelivery {
		printCurrentMessage
		onMsg(command : command(X,Y,DIR)) {
			[# 
				DestSlotX = payloadArg(0).toInt()
				DestSlotY = payloadArg(1).toInt()
				Direction = payloadArg(2).toString()
				
			#]
			
		}
	}Goto pickup
	
	State pickup{
		println("Going to IOPort to pick up product...")
		[#
        	CurrentDestX = IoX
        	CurrentDestY = IoY
      	#]
      	request basicrobot -m moverobot : moverobot($CurrentDestX, $CurrentDestY)
	}
	Transition t2
	whenReply moverobotdone -> delivery
	whenReply moverobotfailed -> handleFailure
	whenInterruptEvent alarm -> handleAnomaly
	whenInterruptEvent anomalyDetected -> handleAnomaly
	
	State delivery{
		println("Delivering product to slot...")
      		[#
        		CurrentDestX = DestSlotX
        		CurrentDestY = DestSlotY
      		#]
      		request basicrobot -m moverobot : moverobot($CurrentDestX, $CurrentDestY)
	}
	Transition t3 
	whenReply moverobotdone -> deposit
	whenReply moverobotfailed -> handleFailure
	whenInterruptEvent alarm -> handleAnomaly
	whenInterruptEvent anomalyDetected -> handleAnomaly
	
	State deposit{
		forward basicrobot -m setdirection : dir($Direction)
		println("depositing product on slot...")
		delay 3000
	}
	Goto return 
	
	State return{
		println("Returning to home position...")
      		[#
        		CurrentDestX = HomeX
        		CurrentDestY = HomeY
      		#]
      		request basicrobot -m moverobot : moverobot($CurrentDestX, $CurrentDestY)
	}
	Transition t4
	whenReply moverobotdone -> endOfTask 
	whenReply moverobotfailed -> handleFailure
	whenInterruptEvent alarm -> handleAnomaly
	whenInterruptEvent anomalyDetected -> handleAnomaly
	
	State endOfTask{
		println("Transport complete. Emitting finishedtransport.")
      	emit finishedtransport : finishedtransport(ok)
	}
	Transition t5
	whenReply moverobotfailed -> handleFailure
	whenInterruptEvent alarm -> handleAnomaly
	whenInterruptEvent anomalyDetected -> handleAnomaly


	State handleFailure {
		println("Robot move failed. Check system status.") color red
		emit finishedtransport : finishedtransport(failure)
	}Goto waitForCommand

	State handleAnomaly {
		println("Anomaly detected. Stopping robot...")
		forward basicrobot -m cmd : cmd(h)
	}Goto waitFix

	State waitFix {
		println("Waiting for anomaly to be fixed...")
		delay 200
	}
	Transition t5 whenEvent anomalyFixed  -> resuming
	
	State resuming{
		request basicrobot -m moverobot : moverobot($CurrentDestX, $CurrentDestY)
		returnFromInterrupt 
	}
	
}
 


QActor sonarmock context ctx_sonarservice{ 
	import "kotlin.random.Random"
	State s0 initial{
		println("$name STARTING") color magenta
	}Goto waitForProduct 
	
	State waitForProduct{
		[#	val randomValue = Random.nextInt(0,100) 	#]
		if[# randomValue/1 >= 75 #]{ 
			emit productDetected : productDetected(T)
			println("$name EMIT product detected") color magenta
		}
		else{
			emit anomalyDetected : anomalyDetected(T)
			println("$name EMIT anomaly detected") color magenta
		}
		delay 100000
	}Goto waitForProduct
	
}




QActor webguimock context ctx_cargoservice{
	State s0 initial{
		println("$name STARTING") color blue
		
	}Goto waitforupdate
	
	State waitforupdate{
		println("waiting for update of gui...") color blue
	}Transition t0 whenMsg update -> updategui
	
	State updategui{
			println("updating webgui...") color blue
	}Goto waitforupdate
}



 
 

